terraform {
  required_providers {
    lxd = {
      source  = "terraform-lxd/lxd"
      version = ">=3.0.0"
    }
    cloudinit = {
      source  = "hashicorp/cloudinit"
      version = ">=2.3.6"
    }
  }
}

resource "lxd_storage_volume" "osd" {
  count        = var.nb_osd
  name         = "${var.hostname}_osd${count.index}"
  pool         = "default"
  type         = "custom"
  content_type = "block"
  config = {
    size = var.osd_disk_size
  }
}

locals {
  fqdn = "${var.hostname}.${var.management_domain}"
}

data "cloudinit_config" "cloudinit-compute" {
  gzip          = false
  base64_encode = false

  part {
    content_type = "text/cloud-config"

    content = var.use_proxy ? templatefile("${path.module}/templates/cloudinit-proxy.yaml", {
      hostname          = var.hostname,
      fqdn              = local.fqdn,
      proxy_url         = var.proxy_url,
      no_proxy          = var.no_proxy,
      management_domain = var.management_domain,
      ssh_public_key    = var.ssh_public_key,
      }) : templatefile("${path.module}/templates/cloudinit.yaml", {
      hostname          = var.hostname,
      fqdn              = local.fqdn,
      management_domain = var.management_domain,
      ssh_public_key    = var.ssh_public_key,
    })
  }
}

resource "lxd_instance" "compute" {
  name  = var.hostname
  image = "ubuntu:noble"
  type  = "virtual-machine"

  timeouts = {
    create = var.vm_boot_timeout
    update = var.vm_boot_timeout
  }

  config = {
    "user.access_interface" = "enp5s0"
    "user.user-data"        = data.cloudinit_config.cloudinit-compute.rendered
    "user.network-config" = var.use_proxy ? templatefile("${path.module}/templates/network-proxy.yaml", {
      proxy_ip       = var.proxy_ip
      management_ip  = var.management_ip
      nameservers    = jsonencode([var.management_dns])
      search_domains = jsonencode([var.management_domain])
      nb_extra_nics  = length(var.compute_nets) + length(var.isolation_nets)
      }) : templatefile("${path.module}/templates/network.yaml", {
      management_ip      = var.management_ip
      management_gateway = var.management_gateway
      nameservers        = jsonencode([var.management_dns])
      search_domains     = jsonencode([var.management_domain])
      nb_extra_nics      = length(var.compute_nets) + length(var.isolation_nets)
    })
    "limits.cpu"    = var.cores
    "limits.memory" = var.memory
  }

  device {
    name = "eth0"
    type = "nic"
    properties = {
      name           = "eth0"
      network        = var.management_net
      "ipv4.address" = var.management_ip
    }
  }

  dynamic "device" {
    for_each = var.compute_nets
    content {
      name = "eth${index(var.compute_nets, device.value) + 1}"
      type = "nic"
      properties = {
        name    = "eth${index(var.compute_nets, device.value) + 1}"
        network = device.value
      }
    }
  }

  dynamic "device" {
    for_each = var.isolation_nets
    content {
      name = "eth${length(var.compute_nets) + index(var.isolation_nets, device.value) + 1}"
      type = "nic"
      properties = {
        name    = "eth${length(var.compute_nets) + index(var.isolation_nets, device.value) + 1}"
        network = device.value
      }
    }
  }

  device {
    name = "root"
    type = "disk"
    properties = {
      path = "/"
      pool = "default"
      size = var.root_disk_size
    }
  }

  dynamic "device" {
    for_each = lxd_storage_volume.osd
    content {
      name = device.value.name

      type = "disk"

      properties = {
        pool   = "default"
        source = device.value.name
      }
    }
  }

  provisioner "local-exec" {
    command     = "sleep 15 && lxc exec ${self.name} -- cloud-init status --wait >/dev/null"
    interpreter = ["bash", "-c"]
  }
}
